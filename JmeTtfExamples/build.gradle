// Gradle script to build and run the JmeTtfExamples subproject of jME-TTF

// Note: "common.gradle" in the root project contains additional initialization
//   for this project. This initialization is applied in the "build.gradle"
//   of the root project.

plugins {
    id 'application' // to build JVM applications
}

application {
    mainClass = 'com.atr.jme.font.example.TestAtlasResize'
}
if (!hasProperty('mainClass')) {
    ext.mainClass = application.mainClass
}
jar.manifest.attributes('Main-Class': application.mainClass)

dependencies {
    runtimeOnly(libs.jme3.desktop)
    runtimeOnly(libs.jme3.lwjgl3)

    // JmeTtfExamples doesn't use jme3-jogg nor jme3-plugins
    //  -- they are included solely to avoid runtime warnings from AssetConfig:
    runtimeOnly(libs.jme3.jogg)
    runtimeOnly(libs.jme3.plugins)

    //implementation 'com.github.stephengold:jme-ttf:' + jmeTtfVersion // for published library
    implementation project(':JmeTtfLibrary') // for local library build
}

import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform
Boolean isMacOS = DefaultNativePlatform.currentOperatingSystem.isMacOsX()

tasks.withType(JavaExec).configureEach { // Java runtime options:
    if (isMacOS) {
        jvmArgs '-XstartOnFirstThread'
    } else {
        args '--showSettingsDialog'
    }
    classpath sourceSets.main.runtimeClasspath
    enableAssertions = true
}

javadoc {
    // Disable doclint for JDK15+ to avoid "no comment" warnings.
    if (JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_15)) {
       options.addStringOption("Xdoclint:none", "-quiet")
    }
}

// Register cleanup tasks:

clean.dependsOn('cleanDLLs', 'cleanDyLibs', 'cleanSOs')

tasks.register('cleanDLLs', Delete) { // extracted Windows native libraries
    delete fileTree(dir: '.', include: '*.dll')
}
tasks.register('cleanDyLibs', Delete) { // extracted macOS native libraries
    delete fileTree(dir: '.', include: '*.dylib')
}
tasks.register('cleanSOs', Delete) { // extracted Linux and Android native libraries
    delete fileTree(dir: '.', include: '*.so')
}

// Register tasks to run specific apps:

tasks.register('runAtlasResize', JavaExec) {
    mainClass = 'com.atr.jme.font.example.TestAtlasResize'
}
tasks.register('runTrueTypeBMP', JavaExec) {
    mainClass = 'com.atr.jme.font.example.TestTrueTypeBMP'
}
tasks.register('runTrueTypeMesh', JavaExec) {
    mainClass = 'com.atr.jme.font.example.TestTrueTypeMesh'
}
